pkgbase=vital-synth
pkgname=("${pkgbase}-standalone-bin" "${pkgbase}-clap-bin" "${pkgbase}-vst-bin" "${pkgbase}-vst3-bin")
pkgver=1.5.5
pkgrel=1
arch=('x86_64')
url="https://vital.audio"
license=('custom')
provides=('vital-synth')
makedepends=("unzip")
depends=('glibc' 'libcurl-gnutls')
source=('Vital Synth.desktop' 'vital-synth.png')
sha256sums=('1e67e84454c128e5af1ccc4313c4e8607996bedcc403e25454052907805dab81'
            '905b2696b7ca634707629433f5d17d2f35b55594e3a63e4a767c17e3d7865f57')

prepare() {
	ln -srf "`xdg-user-dir DOWNLOAD`/VitalInstaller.zip" "${srcdir}/VitalInstaller.zip"
	unzip "${srcdir}/VitalInstaller.zip"
}

package_vital-synth-standalone-bin() {
    pkgdesc="Spectral warping wavetable synth (Standalone)"
    groups=('pro-audio')
    install -Dm755 "$srcdir/VitalInstaller/bin/Vital" "$pkgdir/usr/bin/Vital"
    install -Dm644 "$srcdir/vital-synth.png" "$pkgdir/usr/share/icons/hicolor/256x256/apps/vital-synth.png"
    install -Dm644 "$srcdir/Vital Synth.desktop" "$pkgdir/usr/share/applications/VitalInstaller/bin/Vital Synth.desktop"
}

package_vital-synth-clap-bin() {
    pkgdesc="Spectral warping wavetable synth (CLAP)"
    groups=('pro-audio' 'clap-plugins')
    install -Dm755 "$srcdir/VitalInstaller/lib/clap/Vital.clap" "$pkgdir/usr/lib/clap/Vital.clap"
}

package_vital-synth-vst-bin() {
    pkgdesc="Spectral warping wavetable synth (VST)"
    groups=('pro-audio' 'vst-plugins')
    install -Dm755 "$srcdir/VitalInstaller/lib/vst/Vital.so" "$pkgdir/usr/lib/vst/Vital.so"
}

package_vital-synth-vst3-bin() {
    pkgdesc="Spectral warping wavetable synth (VST)"
    groups=('pro-audio' 'vst3-plugins')
    install -Dm755 "$srcdir/VitalInstaller/lib/vst3/Vital.vst3/Contents/x86_64-linux/Vital.so" "$pkgdir/usr/lib/vst3/Vital.vst3/Contents/x86_64-linux/Vital.so"
}
